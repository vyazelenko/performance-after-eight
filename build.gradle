plugins {
    id "com.github.ben-manes.versions" version "0.39.0"
    id "com.github.johnrengelman.shadow" version "7.0.0" apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects { subproj ->
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        implementation "org.openjdk.jmh:jmh-core:1.33"
        implementation "org.openjdk.jol:jol-core:0.16"
        annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.33"
    }

    shadowJar {
        classifier = 'benchmarks'
        manifest {
            attributes 'Main-Class': 'org.openjdk.jmh.Main'
        }
    }
}

project('jdk-8') {
    sourceCompatibility = targetCompatibility = 1.8
}

project('jdk-11') {
    sourceCompatibility = targetCompatibility = 11

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java'] + project(":jdk-8").sourceSets.main.java.srcDirs
            }
            resources {
                srcDirs = ['src/main/resources'] + project(":jdk-8").sourceSets.main.resources.srcDirs
            }
        }
    }
}
